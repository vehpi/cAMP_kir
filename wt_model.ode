#The wild type model.

########################
# Glycolytic component #
########################

## glucokinase (GK)
p jgk=9.45e-5

## glucose 6-phosphate isomerase (GPI)
!kgpi=19/3

## phosphofructokinase (PFK), adapted from Smolen 1995
p vpfk=9e-4, kpfkbas=.06
p kpfkamp=.04, kpfkfbp=1, kpfkf6p=5e4, kpfkatp=2.5e-4
p famp=.02, fmt=20, ffbp=.2, fbt=20, fatp=20
pow[0..63]=mod(flr(flr([j]/4)*2^(mod([j],4)-3)),2)
w[0..15]=\
(amp/kpfkamp)^shift(pow0,4*[j])*\
(fbp/kpfkfbp)^shift(pow0,4*[j]+1)*\
(f6p^2/kpfkf6p)^shift(pow0,4*[j]+2)*\
(atpc^2/kpfkatp)^shift(pow0,4*[j]+3)/\
famp^(shift(pow0,4*[j])*shift(pow0,4*[j]+2))/\
fmt^(shift(pow0,4*[j])*shift(pow0,4*[j]+3))/\
ffbp^(shift(pow0,4*[j]+1)*shift(pow0,4*[j]+2))/\
fbt^(shift(pow0,4*[j]+1)*shift(pow0,4*[j]+3))/\
fatp^(shift(pow0,4*[j]+2)*shift(pow0,4*[j]+3))
jpfk=vpfk*((1-kpfkbas)*w14 + \
kpfkbas*(w2+w3+w6+w7+w10+w11+w14+w15)) \
/ (sum(0,15)of(shift(w0,i')))


## lower glycolysis (LG)
p sumkp=.1
klg=.5*sumkp

## pyruvate dehydrogenase (PDH)
p vpdh=3.7e-4, kpdhcam=.1, kca=5
cam=kca*cac
jpdh=vpdh*(cam/(cam+kpdhcam))*sqrt(fbp)

#######################################
# ATP production/hydrolysis component #
#######################################

!volmtoc=39/532
p actot=2.5, amtot=15

###AMP and ADP terms are taken from Peercy et al. 2015.

adpc=(atpc/(2*ka1))*(sqrt(1-4*ka1*(1-(actot/atpc)))-1)

atpm=amtot-adpm
aux adpc=adpc

p ka1=0.8,ka2=1
amp=ka1*(adpc^2)/atpc
aux amp=amp


## adenine nucleotide translocator (ANT)
p FRT=.037, vant=7.245e-5, kantam=2, kadpm=12.5, kadpmpdh=.0047, psim=164
adpm=kadpm/exp(jpdh/kadpmpdh)
jant=vant/(1+kantam*adpm/atpm)*exp(FRT/2*psim)


## hydrolysis (hyd)
p khyd=2.34e-5, khydbas=8.1e-6
jhyd=(khyd*cac+khydbas)*atpc


###########################
# Ionic current component #
###########################

p cm=5300

## Ca channels/pumps
p fc=0.0086
p vca=25

### voltage-activated Ca (Ca(V)) current
p gcav=180, vcav=-20 
!hcav=1/12
scavinf=1/(1+exp(vcav-v)^hcav)
icav=gcav*scavinf*(v-vca)

### plasma membrane Ca ATPase (PMCA) flux 
!F=20000000/207
p kpmca=.0414
jmem=-(icav/2/F + kpmca*cac)


### endoplasmic reticulum (ER) flux
!volctoer=620/23
p fer=.01
p kerout=4.14e-5, kerin=.0828
jerout=kerout*(caer-cac)
jerin=kerin*cac
jer=jerout-jerin

## K channels
p vk=-75

### voltage-activated K (K(V)) current
p gkv=486, vkv=-16, hkv=.2
!taukv=1000/9
skvinf=1/(1+exp(vkv-v)^hkv)
ikv=gkv*skv*(v-vk)

### Ca-activated K (K(Ca)) current
p gkca=18, kkcaca=.5, hkcaca=2
skcainf=1/(1+(kkcaca/cac)^hkcaca)
ikca=gkca*skcainf*(v-vk)


### K(ATP), adapted from Magnus and Keizer, 1998
p gkatp=2960
p kdd=17, ktd=26, ktt=1
mgadp=165*adpc
adp3m=135*adpc
atp4m=50*atpc

skatpinf=\
(.08*(1+2*mgadp/kdd) + .89*(mgadp/kdd)^2)/\
((1+mgadp/kdd)^2 * (1+adp3m/ktd+atp4m/ktt))

ikatp=gkatp*skatpinf*(v-vk)


# cAMP component (uM and uM/ms)

# reduced from Yu et al. 

par beta_ac=3, p=3, Kacca=0.08, Kpdeca=0.1
num Kacatp=1400
par vacbar=0.00018, alpha_ac=0.5, vpdebar=0.0024 
par alpha_pde=0.4, beta_pde=1.2, Kpdecamp=3
par Kacamp=0.2

# Adenylyl kinase is inhibited by AMP (4/14)

par alpha_amp=0, beta_amp=1.9, pa=2

vac=vacbar*(alpha_ac+beta_ac*cac^p/(cac^p+Kacca^p))*(alpha_amp + beta_amp*kacamp^pa/(kacamp^pa+amp^pa))
vpde=vpdebar*(alpha_pde+beta_pde*cac^p/(cac^p+Kpdeca^p))*cAMP/(cAMP+Kpdecamp)


##########################
# Differential Equations #
##########################

v'=-(ikv+icav+ikca+ikatp)/cm
skv'=(skvinf-skv)/taukv
cac'=fc*(jmem+jer)
caer'=-fer*volctoer*jer
f6p'=1/(1+kgpi)*(jgk-jpfk)
fbp'=1/(1+klg)*(jpfk-.5*jpdh)
atpc'=volmtoc*jant-jhyd
camp'=vac-vpde


######################
# Initial conditions #
######################

v(0)=-66.1141887342
skv(0)=.0000443655
cac(0)=.1092530273
caer(0)=284.1574308548
f6p(0)=248.3759439254
fbp(0)=0.1741876845
atpc(0)=1.0579152042

@ meth=cvode, toler=1.0e-10, atoler=1.0e-10, dt=20.0, total=1800000
@ maxstor=20000001,bounds=10000000, bell=0, 
@ nplot=2 xp1=tmin, yp1=camp, xp2=tmin, yp2=cac
@ xlo=0, xhi=30, ylo=0, yhi=0.4
@ BUT=QUIT:fq


tmin=t/60000
aux tmin=t/60000

done



